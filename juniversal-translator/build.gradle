/*
 * Copyright (c) 2012-2015, Microsoft Mobile
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */



apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply from: '../shared.gradle'

group = 'org.juniversal'
version = juniversalVersion

buildscript {
    repositories {
        jcenter()
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')

    compile group: 'com.intellij', name: 'annotations', version:'9.0.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    // Include sources too, when upload to Maven Central
    archives javadocJar, sourcesJar
}

// Sign all artifacts
signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

jar {
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes 'Implementation-Title': "JUniversal Translator",
                'Implementation-Version': "${version} build ${buildNumber}",
                'Main-Class': 'org.juniversal.translator.core.Translator',
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
    }
}

// Note that for the signing and uploading to work, the following must be defined in gradle.properties
// Normally that's only set on the JUniversal build machine, as those are the only builds we want published
// to Maven Central.   For everyone else, just don't run uploadArchives
//
// Sample gradle.properties for JUniversal build machine:
// signing.keyId=YourKeyId
// signing.password=YourPublicKeyPassword
// signing.secretKeyRingFile=PathToYourKeyRingFile
//
// ossrhUsernameJUniversal=your-jira-id
// ossrhPasswordJUniversal=your-jira-password

uploadArchives {
    repositories {

        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // To publish translator updates to our Maven Central repo, ossrhUsernameJUniversal and ossrhPasswordJUniversal should
            // be defined in ~/.gradle/gradle.properties, with our credential info.   Do the hasProperty to check
            // for that
            if (project.hasProperty('ossrhUsernameJUniversal') && project.hasProperty('ossrhPasswordJUniversal')) {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsernameJUniversal, password: ossrhPasswordJUniversal)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsernameJUniversal, password: ossrhPasswordJUniversal)
                }
            }

            pom.project {
                name 'JUniversal Translator'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Translate Java to C# or C++'
                url 'https://github.com/juniversal/juniversal'

                scm {
                    connection 'scm:git:git@github.com:juniversal/juniversal.git'
                    developerConnection 'scm:git:git@github.com:juniversal/juniversal.git'
                    url 'git@github.com:juniversal/juniversal.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        name 'Bret Johnson'
                        email 'bret.johnson@microsoft.com'
                    }

                    developer {
                        name 'Chris Ingram'
                        email 'chris.ingram@microsoft.com'
                    }
                }
            }
        }
    }
}

// In Java8, the Javadoc command was changed to be very strict, error'ing out on anything that's not completely
// valid HTML, an issue described here http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html.
// Loosen up the strictness with Xdoclint:none so it behaves more like previous JDK releases
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


